#!/usr/bin/env perl
use Mojolicious::Lite -signatures;
use Mojo::File       qw/path/;
use Mojo::Collection qw/c/;

my $data_dir = app->home->child('data');

my $default_zoom = 5;
my $default_lat  = 37.09;
my $default_lon  = -96.7;

app->defaults(zoom => $default_zoom);

package Geofile {
  use Mojo::Base qw/-base -signatures/;
  use Mojo::File qw/path/;
  use Text::CSV_XS;

  has 'geodata';
  has 'header';
  has 'filename';

  sub name($self) {
    path($self->filename)->basename('.csv');
  }

  sub ingest($self, $name) {
    my ($header, $data) = read_file($name);
    $self->new(header => $header, geodata => $data, filename => $name);
  }

  sub read_file($name) {
    my $csv = Text::CSV_XS->new();
    open my $fh, "<", "$name" or die "$name: $!";
    my %seen;
    my @header = $csv->header(
      $fh,
      { munge_column_names =>
          sub { $seen{$_[0]}++ ? $_[0] . '-' . $seen{$_[0]} : $_[0] }
      }
    );
    $csv->parse("$name") or die $!;
    my $data = $csv->getline_hr_all($fh);
    close $fh;
    (\@header, $data);
  }
}

get '/' => sub($c) {
  $c->stash(zoom     => $c->param('zoom') || $default_zoom);
  $c->stash(lat      => $c->param('lat')  || $default_lat);
  $c->stash(lon      => $c->param('lon')  || $default_lon);
  $c->stash(geofiles => []);
  $c->stash(
    csv_files => [
      $data_dir->list({dir => 1})->grep(sub { /.csv/ || -d "$_" })->sort->each
    ]
  );
} => 'index';

get '/show/csv/:name' => sub($c) {
  $c->stash(zoom      => $c->param('zoom') || $default_zoom);
  $c->stash(lat       => $c->param('lat')  || $default_lat);
  $c->stash(lon       => $c->param('lon')  || $default_lon);
  $c->stash(csv_files => []);
  my $name = $c->stash('name');
  my $io   = $data_dir->child("$name");
  if (-d $io) {
    $c->stash(geofiles =>
        $io->list->grep(sub { /\.csv/ })->map(sub { Geofile->ingest("$_") }));
  } else {
    $c->stash(geofiles => [Geofile->ingest("$io.csv")]);
  }
  $c->render(template => 'index');
} => 'show';

app->start;
__DATA__

@@ index.html.ep
% use Mojo::Collection qw/c/;
% layout 'default';
% title 'leafpad';

<h3>
%= link_to 'leafpad' => 'index'
</h3>

<div class="csv_files">
  % for my $file (@$csv_files) {
    %= link_to url_for('show', { name => $file->basename('.csv') }) => begin
      %= $file->basename('.csv')
    %= end
  % }
</div>

<div class='controls'>
  <div id='current_link'>
  Press <i>l</i> to generate a link to the current zoom+location
  </div>
  <div class='current_pos' title='latitude,longitude' alt='latitude,longitude'>
    <span id='lat'></span>, <span id='lon'></span>
  </div>
</div>

<div id='details'>...</div>

<div class='panels'>
  <div id="map"></div>
  <div id='csv_data'>
    <div id='tabs'>
      % for my $file (@$geofiles) {
        <div id='<%= $file->name %>'><%= $file->name %></div>
      % }
    </div>
    <div id='csv_tables'>
    % for my $file (@$geofiles) {
      <table class='csv_data' id='table_<%= $file->name %>'>
        <caption><b><%= $file->name %></b></caption>
        <tr>
          % for my $col (@{ $file->header }) {
          <th><%= $col %></th>
          % }
        </tr>
        % my $row_index = 0;
        % for my $row (@{ $file->geodata }) {
          <tr>
            % for my $col (@{ $file->header }) {
              <td>
                <div class='csv_cell'
                     data-csv_file="<%= $file->name %>"
                     data-csv_col="<%= $col %>"
                     data-csv_row="<%= $row_index %>"><%= $row->{$col} %></div>
                </td>
            % }
            % $row_index++;
          </tr>
        % }
      </table>
    % }
    </div>
  </div>
</div>

<style>
  #map { }
</style>
%= javascript begin

var highlight = {
    "fillColor": "#000000",
    "color": "#ff7A00",
    "weight" : 10,
    "opacity": 0.4
};

var geostyle = {
    "fillColor": "#ccfaa0",
    "fillOpacity" : 0.1,
    "color": "#000000",
    "weight": 1,
    "radius": 3,
    "opacity": 0.9
};

var markerstyle = {
  radius: 5,
  weight: 1,
  fillOpacity: 0.8,
  fillColor: "#aacc00",
  color: "#000000"
};

function generate_link() {
  let el = document.getElementById('current_link');
  let base = '<%= url_for->to_abs %>';
  let latlng = map.getCenter()
  let link = `${base}?lat=${latlng.lat}&lon=${latlng.lng}&zoom=${map.getZoom()}`
  el.innerHTML = `<a href=${link}>${link}</a>`
}

var map = L.map('map').setView([<%= $lat %>, <%= $lon %>], <%= $zoom =%>);
map.doubleClickZoom.disable();
L.tileLayer.provider('CartoDB.Positron').addTo(map);
L.control.scale().addTo(map);

let highlighted = null;
var geolayer;
var all_layers = {}
% for my $file (@{ $geofiles }) {
  all_layers['<%= $file->name %>'] = {}
  % my $geodata = $file->geodata;
  % my $csv_row = 0;
  % for (@$geodata) {
    all_layers['<%= $file->name %>']['<%= $csv_row %>'] = {}
    % for my $col (keys %$_) {
       % next unless $col =~ /geojson/;
       % my $geo = $_->{$col} or next;
       % if ( $geo =~ /Feature/ ) {
         geolayer = L.geoJSON( <%== $_->{$col} %>, { style: geostyle } )
       % } else {
         geolayer = L.geoJSON({
                 "type": "Feature",
                 "interactive" : false,
                  "style" : { color: "black" },
                  "geometry" : <%== $_->{$col} %>
             }, { style: geostyle, pointToLayer: function (f,latlng) { return L.circleMarker(latlng,geostyle) } })
       % }
       geolayer.on('mouseover', function() {
         highlighted = this; this.setStyle(highlight);
         let j = <%== Mojo::JSON::encode_json($_) =%>;
         document.getElementById('details').innerHTML = make_details(j);
       })
       geolayer.on('mouseout', function() { this.resetStyle() })
       geolayer.on('click', function() { this.resetStyle(); this.bringToBack(); })
       all_layers['<%= $file->name %>']['<%= $csv_row %>']['<%= $col %>'] = geolayer;
       geolayer.csv_row = '<%= $csv_row %>'
       geolayer.csv_col = '<%= $col %>'
       geolayer.csv_file = '<%= $file->name %>'
       geolayer.addTo(map);
     % }
     % $csv_row++;
  % }

  % for (@$geodata) {
    % my $lat = $_->{lat} || $_->{latitude} or next;
    % my $lon = $_->{lon} || $_->{longitude} or next;
    j = <%== Mojo::JSON::encode_json($_) =%>;
    L.circleMarker([<%= $lat %>, <%= $lon %>], markerstyle).addTo(map).bindPopup(make_details(j), {maxWidth: "auto"})
  % }
% }

function make_details(j) {
 let out = '<table>'
 for (let k of Object.keys(j)) {
  if (k.match(/geojson$/)) {
    continue;
  }
  out += "<tr><td>" + k +  "</td><td>"
  if (j[k].length > 50) {
     out += j[k].substr(0,50) + '...'
     console.log(`${k} is`,j[k])
  } else {
     out += j[k]
  }
  out += "</td></tr>"
 }
 out += "</table>"
 return out
}

const mouselistener = (event) => {
  document.getElementById('lat').innerHTML = Math.round(event.latlng.lat * 10000000) / 10000000;
  document.getElementById('lon').innerHTML = Math.round(event.latlng.lng * 10000000) / 10000000;
}

const keylistener = (event) => {
  if (event.ctrlKey || event.altKey || event.metaKey || event.shiftKey)
    return
  const keyName = event.key;
  if (keyName === 'l')
    generate_link()
}

let last_cell = null
const csvlistener = (e) => {
  let cell = e.target
  let data = cell.dataset
  let file = data.csv_file
  if (!file) return
  let layer = all_layers[file][data.csv_row][data.csv_col]
  if (!layer) return

  map.flyToBounds(layer, { maxZoom: 17 })

  if (last_cell)    last_cell.style.backgroundColor = 'white'
  if (highlighted)  highlighted.resetStyle() 
  cell.style.backgroundColor = '#ddddff'
  last_cell                  = cell
  highlighted                = layer
  layer.setStyle(highlight)
}

const tablistener = (e) => {
  let table_to_show = `table_${e.target.id}`
  let tab_to_show   = e.target.id
  for (let c of document.querySelector('#csv_tables').children) {
    c.style.display = c.id == table_to_show ? 'block' : 'none'
  }
  for (let c of document.querySelector('#tabs').children) {
    c.style.backgroundColor = c.id == tab_to_show ? '#00bb00' : 'white'
    c.style.color           = c.id == tab_to_show ? 'white' : 'black'
  }
}

window.onload = () => {
  document.addEventListener('keydown', keylistener)
  document.getElementById('csv_data').addEventListener('click', csvlistener);
  document.getElementById('tabs').addEventListener('click', tablistener);
  map.addEventListener('mousemove', mouselistener)
}

%= end

@@ style.css
div#csv_data {
  overflow-y: scroll;
  border:1px solid #888;
}
.panels {
  display:grid;
  grid-template-rows: 70vh 20vh;
  grid-template-columns: 1fr;
}
#details {
  border: 1px solid grey;
  padding: 2px;
  margin: 4px;
  box-shadow: 1px 2px 4px #888888;
  position:absolute;
  top:0;
  right:0;
  background-color:white;
}
#map {
  border:1px solid #888;
  cursor:crosshair;
}
table {
  border-collapse: collapse;
  border-color: #ddd;
}
td {
  border: 1px solid #ddd;
}
div.controls {
  position:relative;
}
div.current_pos {
  position:absolute;
  top:0px;
  right:2px;
}
div.csv_files a {
  border:1px solid #99aa99;
  display:inline-block;
  margin-left:1px;
  margin-bottom:1px;
  padding-left:1px;
  padding-right:1px;
  whitespace:no-break;
  background-color:#ddffdd;
  text-decoration:none;
  color:black;
}
a {
  text-decoration:none;
  color:#004400;
}
div#tabs div {
  display:inline-block;
  border:1px solid #0b0;
  padding-left:2px;
  padding-right:2px;
}
div#tabs div:hover {
  display:inline-block;
  border:1px solid #0b0;
  background-color: #0b0;
  color:white;
  padding-left:2px;
  padding-right:2px;
}

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
<head>
  <link rel="stylesheet" href="https://unpkg.com/leaflet@1.8.0/dist/leaflet.css"
     integrity="sha512-hoalWLoI8r4UszCkZ5kL8vayOGVae1oxXe/2A4AO6J9+580uKHDO3JdHb7NzwwzK5xr/Fs0W40kiNHxM9vyTtQ=="
     crossorigin=""/>
  <script src="https://unpkg.com/leaflet@1.8.0/dist/leaflet.js"
     integrity="sha512-BB3hKbKWOc9Ez/TAwyWxNXeoV9c1v6FIeYiBieIWkpLjauysF18NzgR1MBNBXf8/KABdlkX68nAhlwcDFLGPCQ=="
     crossorigin=""></script>
    <script src="https://unpkg.com/leaflet-providers@1.13.0/leaflet-providers.js"></script>
  %= stylesheet '/style.css'
  <title><%= title %></title>
</head>
  <body><%= content %></body>
</html>
